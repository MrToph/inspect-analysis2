#if DEBUG
	.output PossibleFakeToken
	.output ManualCheck
#endif

.output FakeTokenViolation


/* Issues:
1. Actions named "transfer" are identified as an on_notify action, 
which triggers the pattern.

*/



.decl PossibleFakeToken(func: Function)

PossibleFakeToken(action):-
	OnTransferAction(call, action),
	!OnTransferEOSAction(call, action).

.decl ManualCheck(insn: Insn)

ManualCheck(insn):-
	_AssignType(insn, to, "Ne"),
	_AssignVar(insn, to, from),
	pointer.code.VarPointsToParam(from).



.decl SafeToken(action: Function)

SafeToken(action):-
	PossibleFakeToken(action),
	_FuncInsn(action, insn),
	ManualCheck(insn).

SafeToken(action):-
	PossibleFakeToken(action),
	ActionFuncReachable(action, func, _),
	_FuncInsn(func, insn),
	ManualCheck(insn).

.decl FakeTokenViolation(action: Function)
FakeTokenViolation(action):-
	PossibleFakeToken(action),
	!SafeToken(action).
